buildscript {
    ext.kotlin_version = "1.0.3"
    ext.spring_boot_version = "1.4.0.RELEASE"

    repositories {
        mavenCentral()
        jcenter()
        maven {
          url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: "kotlin"
apply plugin: "idea"
apply plugin: "spring-boot"
apply plugin: "application"
apply plugin: 'com.moowork.node'

mainClassName = "kaffe.ApplicationKt"
applicationName = "KaffeApp"

task copyHerokuProperties(type: Copy) {
    from "config_sources"
    into "config"
    include "application_heroku.properties"
    rename("application_heroku.properties", "application.properties")
}

task copyLocalMLabProperties(type: Copy) {
    from "config_sources"
    into "config"
    include "application_local_mlab.properties"
    rename("application_local_mlab.properties", "application.properties")
}

task copyLocalMongoProperties(type: Copy) {
    from "config_sources"
    into "config"
    include "application_local_mongo.properties"
    rename("application_local_mongo.properties", "application.properties")
}

task webpack(type: Exec) {
    commandLine "$projectDir/node_modules/.bin/webpack"
}

task stage(dependsOn: ['copyHerokuProperties', 'npmInstall', 'webpack', 'clean', 'installDist'])


sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
}


repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-devtools"
}

springBoot {
    mainClass = 'kaffe.ApplicationKt'
}

// Used by 'Spring Boot Gradle Plugin' in combination with 'Devtools' makes HTML/JS/CSS reloadable.
bootRun {
    addResources = true
}